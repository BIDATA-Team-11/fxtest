plugins {
  id 'java'
  id 'maven'
  id 'application'
  id "com.github.bdeneuter.mindstorms.ev3" version "1.2.3" // LejOS
  id 'org.openjfx.javafxplugin' version '0.0.8' // OpenJFX - GUI
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'team11'
version = '1.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  implementation 'com.google.guava:guava:28.0-jre' // This dependency is used by the application.
  testImplementation 'junit:junit:4.12' // Use JUnit test framework
}

application {
  mainClassName = 'fx.App'
}

javadoc {
  exclude "**/lejos/**"
  source = sourceSets.main.allJava
}

javafx {
  version = "11.0.2"
  modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mindstorms {
  main = 'fx.App'
  ip = '10.0.1.1'
  user = 'root'
  password = ''
  home = '/home/lejos/programs'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'GNU General Public License, Version 3'
          url 'http://www.gnu.org/licenses/'
          distribution 'repo'
        }
      }
    }
  }
}
